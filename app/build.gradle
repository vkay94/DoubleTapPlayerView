apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

apply from: '../dependencies.gradle'

android {
    compileSdkVersion build.compileSdk
    buildToolsVersion build.buildTools
    defaultConfig {
        applicationId "com.github.vkay94.doubletapplayerviewexample"
        minSdkVersion build.minSdk
        targetSdkVersion build.targetSdk
        versionCode build.libMajor + (build.libMinor * 100) + (build.libPatch * 10000)
        versionName "$build.libMajor.$build.libMinor.$build.libPatch"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    signingConfigs {
        release {
            def keystorePropertiesFile = rootProject.file("keystore.properties")

            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            // shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            // signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$versions.kotlin"
    implementation "androidx.appcompat:appcompat:$versions.appcompat"
    implementation "androidx.core:core-ktx:$versions.core_ktx"
    implementation "androidx.constraintlayout:constraintlayout:$versions.constraintlayout"

    // ExoPlayer2
    implementation "com.google.android.exoplayer:exoplayer-core:$versions.exoplayer"
    implementation "com.google.android.exoplayer:exoplayer-ui:$versions.exoplayer"

    implementation "com.github.StephenVinouze:MaterialNumberPicker:1.1.0"
    implementation project(path: ':doubletapplayerview')

    implementation "com.google.android.material:material:$versions.material"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.multidex:multidex:2.0.1'
}
